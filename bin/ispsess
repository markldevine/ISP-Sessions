#!/usr/bin/env raku

use ISP::Server::Reporter;

class ISP-Sessions-statistics {

    constant \KILO      = 1024;
    constant \MEGA      = KILO      * 1024;
    constant \GIGA      = MEGA      * 1024;
    constant \TERA      = GIGA      * 1024;
    constant \PETA      = TERA      * 1024;

    constant \MINUTE    = 60;
    constant \HOUR      = MINUTE    * 60;
    constant \DAY       = HOUR      * 24;

    method seconds-to-d-hh-mm-ss (Int:D $total-seconds) {
        my $dhms        = 'hh:mm:ss';
        my $days        = 0;
        my $hours       = 0;
        my $minutes     = 0;
        my $seconds     = $total-seconds;
        my $result;
        if $seconds >= DAY {
            $days       = ($seconds / DAY).Int;
            $result     = $days ~ 'd ';
            $seconds   -= $days * DAY;
        }
        if $seconds >= HOUR {
            $hours      = ($seconds / HOUR).Int;
            $result    ~= $hours.fmt("%02d");
            $seconds   -= $hours * HOUR;
        }
        else {
            $result    ~= '00';
        }
        if $seconds >= MINUTE {
            $minutes    = ($seconds / MINUTE).Int;
            $result    ~= ':' ~ $minutes.fmt("%02d") ~ ':';
            $seconds   -= $minutes * MINUTE;
        }
        else {
            $result    ~= ':00:';
        }
        $seconds        = 0 if $seconds < 0;
        $result        ~= $seconds.fmt("%02d");
        return $result;
    }

    has Int $!bytes-sent            = 0;
    has Int $.bytes-sent-delta      = 0;
    has Int $!last-bytes-sent       = -1;

    method to-bytes (Str:D $num-unit) {
        if $num-unit ~~ / ^ (\d+ '.'* \d*) \s* (\w*) $ / {
            my $actual  = $0.Str.comb.grep(/ \d | '.' /).join;
            my $unit    = $1.Str with $1;
            given $unit {
                when 'K'    { return $actual * KILO }
                when 'M'    { return $actual * MEGA }
                when 'G'    { return $actual * GIGA }
                when 'T'    { return $actual * TERA }
                when 'P'    { return $actual * PETA }
                default     { return $actual;       }
            }
        }
        return $num-unit;
    }

    method bytes-to-unit (Int:D $bytes) {
        given $bytes {
            when $_ >= PETA { return ($bytes / PETA).fmt("%.1f P"); }
            when $_ >= TERA { return ($bytes / TERA).fmt("%.1f T"); }
            when $_ >= GIGA { return ($bytes / GIGA).fmt("%.1f G"); }
            when $_ >= MEGA { return ($bytes / MEGA).fmt("%.1f M"); }
            when $_ >= KILO { return ($bytes / KILO).fmt("%.1f K"); }
            default         { return $bytes.fmt("%s B");            }
        }
    }

    method bytes-sent (Int :$BYTES_SENT) {
        return $!bytes-sent         without $BYTES_SENT;
        my $actual-bytes-sent       = $BYTES_SENT;
        $!last-bytes-sent           = $actual-bytes-sent unless $!last-bytes-sent >= 0;
        $!bytes-sent-delta          = $actual-bytes-sent - $!last-bytes-sent;
        $!bytes-sent                = $actual-bytes-sent;
        $!last-bytes-sent           = $actual-bytes-sent;
    }

    has Int $!bytes-received        = 0;
    has Int $.bytes-received-delta  = 0;
    has Int $!last-bytes-received   = -1;

    method bytes-received (Int :$BYTES_RECEIVED) {
        return $!bytes-received     without $BYTES_RECEIVED;
        my $actual-bytes-received   = $BYTES_RECEIVED;
        $!last-bytes-received       = $actual-bytes-received unless $!last-bytes-received >= 0;
        $!bytes-received-delta      = $actual-bytes-received - $!last-bytes-received;
        $!bytes-received            = $actual-bytes-received;
        $!last-bytes-received       = $actual-bytes-received;
    }
}

class Reporter does ISP::Server::Reporter {

    has %!statistics;

    has $.client-name;

    method process-rows (@sessions) {
        my Int      $SESSION_ID;                            # 872694
        my DateTime $START_TIME;                            # 2023-02-25 15:05:15.000000
        my Str      $COMMMETHOD;                            # SSL
        my Str      $STATE;                                 # RecvW
        my Int      $WAIT_SECONDS;                          # 0
        my Int      $BYTES_SENT;                            # 1277
        my Int      $BYTES_RECEIVED;                        # 1123077711
        my Str      $SESSION_TYPE;                          # Node
        my Str      $CLIENT_PLATFORM;                       # TDP Oracle AIX
        my Str      $CLIENT_NAME;                           # CADBBUSDR10-NBUSPRD1-TDP
        my Str      $OWNER_NAME;                            #
        my Str      $MOUNT_POINT_WAIT;                      #
        my Str      $INPUT_MOUNT_WAIT;                      #
        my Str      $INPUT_VOL_WAIT;                        #
        my Str      $INPUT_VOL_ACCESS;                      #
        my Str      $OUTPUT_MOUNT_WAIT;                     #
        my Str      $OUTPUT_VOL_WAIT;                       #
        my Str      $OUTPUT_VOL_ACCESS;                     # PRMANDRSTG,/tsmstg11/00050042.BFS,142
        my Str      $LAST_VERB;                             # ConfirmResp
        my Str      $VERB_STATE;                            # Sent
        my Str      $FAIL_OVER_MODE;                        # No

        my $row;
        for @sessions -> $session {
            $CLIENT_NAME        = Nil;  $CLIENT_NAME        = $session{'CLIENT_NAME'}.Str       if $session{'CLIENT_NAME'};
            if self.client-name {
                next unless self.client-name.fc eq $CLIENT_NAME.fc;
            }
            $SESSION_ID         = 0;    $SESSION_ID         = $session{'SESSION_ID'}.Int        if $session{'SESSION_ID'};
            $START_TIME         = Nil;  $START_TIME         = $session{'START_TIME'}.DateTime   if $session{'START_TIME'};
            $COMMMETHOD         = ' ';  $COMMMETHOD         = $session{'COMMMETHOD'}.Str        if $session{'COMMMETHOD'};
            $STATE              = ' ';  $STATE              = $session{'STATE'}.Str             if $session{'STATE'};
            $WAIT_SECONDS       = 0;    $WAIT_SECONDS       = $session{'WAIT_SECONDS'}.Int      if $session{'WAIT_SECONDS'};
            $BYTES_SENT         = 0;    $BYTES_SENT         = $session{'BYTES_SENT'}.Int        if $session{'BYTES_SENT'};
            $BYTES_RECEIVED     = 0;    $BYTES_RECEIVED     = $session{'BYTES_RECEIVED'}.Int    if $session{'BYTES_RECEIVED'};
            $SESSION_TYPE       = ' ';  $SESSION_TYPE       = $session{'SESSION_TYPE'}.Str      if $session{'SESSION_TYPE'};
            $CLIENT_PLATFORM    = ' ';  $CLIENT_PLATFORM    = $session{'CLIENT_PLATFORM'}.Str   if $session{'CLIENT_PLATFORM'};
            $CLIENT_NAME        = ' ';  $CLIENT_NAME        = $session{'CLIENT_NAME'}.Str       if $session{'CLIENT_NAME'};
            $OWNER_NAME         = ' ';  $OWNER_NAME         = $session{'OWNER_NAME'}.Str        if $session{'OWNER_NAME'};
            $MOUNT_POINT_WAIT   = ' ';  $MOUNT_POINT_WAIT   = $session{'MOUNT_POINT_WAIT'}.Str  if $session{'MOUNT_POINT_WAIT'};
            $INPUT_MOUNT_WAIT   = ' ';  $INPUT_MOUNT_WAIT   = $session{'INPUT_MOUNT_WAIT'}.Str  if $session{'INPUT_MOUNT_WAIT'};
            $INPUT_VOL_WAIT     = ' ';  $INPUT_VOL_WAIT     = $session{'INPUT_VOL_WAIT'}.Str    if $session{'INPUT_VOL_WAIT'};
            $INPUT_VOL_ACCESS   = ' ';  $INPUT_VOL_ACCESS   = $session{'INPUT_VOL_ACCESS'}.Str  if $session{'INPUT_VOL_ACCESS'};
            $OUTPUT_MOUNT_WAIT  = ' ';  $OUTPUT_MOUNT_WAIT  = $session{'OUTPUT_MOUNT_WAIT'}.Str if $session{'OUTPUT_MOUNT_WAIT'};
            $OUTPUT_VOL_WAIT    = ' ';  $OUTPUT_VOL_WAIT    = $session{'OUTPUT_VOL_WAIT'}.Str   if $session{'OUTPUT_VOL_WAIT'};
            $OUTPUT_VOL_ACCESS  = ' ';  $OUTPUT_VOL_ACCESS  = $session{'OUTPUT_VOL_ACCESS'}.Str if $session{'OUTPUT_VOL_ACCESS'};
            $LAST_VERB          = ' ';  $LAST_VERB          = $session{'LAST_VERB'}.Str         if $session{'LAST_VERB'};
            $VERB_STATE         = ' ';  $VERB_STATE         = $session{'VERB_STATE'}.Str        if $session{'VERB_STATE'};
            $FAIL_OVER_MODE     = ' ';  $FAIL_OVER_MODE     = $session{'FAIL_OVER_MODE'}.Str    if $session{'FAIL_OVER_MODE'};
            %!statistics{$SESSION_ID}                       = ISP-Sessions-statistics.new   unless %!statistics{$SESSION_ID}:exists;
            %!statistics{$SESSION_ID}.bytes-sent(:$BYTES_SENT);
            %!statistics{$SESSION_ID}.bytes-received(:$BYTES_RECEIVED);
            $row                = Array.new;
            $row.push:          $SESSION_ID;
            my $active          = '';
            if $START_TIME {
                my $current-dt  = DateTime.new(now, :timezone(self.seconds-offset-UTC));
                $active         = %!statistics{$SESSION_ID}.seconds-to-d-hh-mm-ss(($current-dt.local - $START_TIME).Int);
            }
            $row.push:          $active;
            $row.push:          $COMMMETHOD;
            $row.push:          $SESSION_TYPE;
            $row.push:          $CLIENT_PLATFORM;
            if $WAIT_SECONDS {
                $row.push:      '[' ~ %!statistics{$SESSION_ID}.seconds-to-d-hh-mm-ss($WAIT_SECONDS) ~ '] ' ~ $STATE.fmt("%6s");
            }
            else {
                $row.push:      $STATE;
            }
            if %!statistics{$SESSION_ID}.bytes-sent-delta {
                $row.push:      sprintf("%10s%9s", '[' ~ "\x[0394] " ~ %!statistics{$SESSION_ID}.bytes-to-unit(%!statistics{$SESSION_ID}.bytes-sent-delta) ~ ']',
#               $row.push:      sprintf("%10s%9s", '[' ~ %!statistics{$SESSION_ID}.bytes-to-unit(%!statistics{$SESSION_ID}.bytes-sent-delta) ~ ']',
                                                    %!statistics{$SESSION_ID}.bytes-to-unit(%!statistics{$SESSION_ID}.bytes-sent),
                                       );
            }
            else {
                $row.push:      sprintf("%20s", %!statistics{$SESSION_ID}.bytes-to-unit(%!statistics{$SESSION_ID}.bytes-sent));
            }
            if %!statistics{$SESSION_ID}.bytes-received-delta {
                $row.push:      sprintf("%10s%9s", '[' ~ "\x[0394] " ~ %!statistics{$SESSION_ID}.bytes-to-unit(%!statistics{$SESSION_ID}.bytes-received-delta) ~ ']',
#               $row.push:      sprintf("%10s%9s", '[' ~ %!statistics{$SESSION_ID}.bytes-to-unit(%!statistics{$SESSION_ID}.bytes-received-delta) ~ ']',
                                                    %!statistics{$SESSION_ID}.bytes-to-unit(%!statistics{$SESSION_ID}.bytes-received),
                                       );
            }
            else {
                $row.push:      sprintf("%20s", %!statistics{$SESSION_ID}.bytes-to-unit(%!statistics{$SESSION_ID}.bytes-received));
            }
            my $record          =       $CLIENT_NAME;
            $record            ~=       '@FO'   if $FAIL_OVER_MODE eq 'Yes';
            $row.push:          $record;
#dd $row;
            self.table.add-row: $row;
        }
    }
}

sub MAIN (
    Str:D   :$isp-server!,                          #= ISP server name
    Str:D   :$isp-admin!,                           #= ISP server name
    Int:D   :$interval      where * >= 5    = 58,   #= Refresh every --interval seconds (minimum 5s)
    Int:D   :$count                         = 1,    #= Number of refreshes (0 is infinity)
    Bool    :$grid,                                 #= Full table grid
    Bool    :$clear,                                #= Clear the screen with each iteration
    Str     :$client-name,                          #= ISP CLIENT/NODE name
) {
    my @command     = ['SELECT', '*', 'FROM', 'SESSIONS'];
    my @fields;
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Session'),          :alignment('c'));
#   @fields.push:   ISP::Server::Reporter::Field.new(:name("Active \x[029D6]"), :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name("Active"), :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Comm'),             :alignment('c'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Type'),             :alignment('c'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Platform'),         :alignment('c'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('State'),            :alignment('r'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Bytes Sent'),       :alignment('c'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Bytes Received'),   :alignment('c'));
    @fields.push:   ISP::Server::Reporter::Field.new(:name('Client'),           :alignment('r'));
    my $reporter    = Reporter.new(
                                    :$isp-server,
                                    :$isp-admin,
                                    :$count,
                                    :$grid,
                                    :$clear,
                                    :$interval,
                                    :title('IBM Spectrum Protect: ' ~ $isp-server ~ ' Sessions'),
                                    :@command,
                                    :@fields,
                                    :$client-name,
                                    :sort-by('Session'),
                                  );
    $reporter.loop;
}

=finish
